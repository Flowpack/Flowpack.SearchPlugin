
prototype(Flowpack.SearchPlugin:Search) {

	// we store the search results in the TypoScript context, so that they bubble through the nested elements like the searchResultRenderer
	@override.searchResults = ${this.searchQuery.execute()}


	searchQuery = ${Search.query(site).fulltext(request.arguments.search || '')}

	totalSearchResults = ${this.searchQuery.count()}
	searchResults = ${searchResults}

	searchForm = Flowpack.SearchPlugin:Search.Form

	searchResultRenderer = TYPO3.TypoScript:Collection {
		collection = ${searchResults}
		itemRenderer = Flowpack.SearchPlugin:SingleResult
		itemName = 'node'
	}

	searchResultRenderer.@process.wrap = ${'<ul>' + value + '</ul>'}
	prototype(Flowpack.SearchPlugin:SingleResult).@process.wrap = ${'<li>' + value + '</li>'}

	@cache {
		mode = 'uncached'
		context {
			1 = 'site'
			2 = 'node'
		}
	}
}


prototype(Flowpack.SearchPlugin:Search.Form) < prototype(TYPO3.TypoScript:Template) {
	templatePath = 'resource://Flowpack.SearchPlugin/Private/Templates/NodeTypes/Search.Form.Html'
	searchWord = ${request.arguments.search}
}

