prototype(Flowpack.SearchPlugin:SingleResult) < prototype(Neos.Fusion:Case) {
    default {
        condition = Flowpack.SearchPlugin:CanRender {
            type = ${node.nodeType.name + 'SearchResult'}
        }
        type = ${node.nodeType.name + 'SearchResult'}
    }

    fallback {
        condition = TRUE
        type = 'Neos.Neos:DocumentSearchResult'
    }
}

prototype(Neos.Neos:DocumentSearchResult) < prototype(Neos.Fusion:Template) {
    templatePath = 'resource://Flowpack.SearchPlugin/Private/Templates/SearchResult/DocumentSearchResult.html'
    node = ${node}
    highlight = ${Flowpack.SearchPlugin.Array.flatten(searchHit.highlight)}

    title = ${q(node).property('title')}
    description = ''
    parents = ${Array.reverse(q(node).parents('[instanceof Neos.Neos:Document]').get())}
}

prototype(Neos.NodeTypes:PageSearchResult) < prototype(Neos.Neos:DocumentSearchResult)
